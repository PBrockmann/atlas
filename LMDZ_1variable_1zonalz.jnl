
!**************************************************************
! Author: Patrick Brockmann
! Contact: Patrick.Brockmann@ipsl.jussieu.fr
! $Date: 2011/09/12 12:17:42 $
! $Name: ATLAS_672_1_1 $
! $Revision: 1.1.1.10 $
! History:
! Modification:
!
!**************************************************************

set memory/size=20

!************************************************************
def sym file=$1
def sym variable1=$2
def sym qualifiers=/nolab/nokey/title=""$3% %
def sym method=$4%shade%
def sym surface_pressure_name=$5%PSOL%

!************************************************************
let variable1=($variable1)

use "($file)"
go symbol_basename_and_define basefile "($file)"

go page_new -i 1 1 0 100 10 90

set v 1
go margins_set 10 10 10 10
def sym variable1_lstart=`variable1,return=lstart`
def sym variable1_lend=`variable1,return=lend`
if `($variable1_lstart) ne ($variable1_lend)` then
   say "Variable1 must have a time dimension equals to 1"
   exit
endif
go LMDZ_define_pressure ($surface_pressure_name)[l=($variable1_lstart),d=($basefile)]
let variable1z = ZAXREPLACE(variable1[d=($basefile)],pressure,Z[GZ=LMDZ_stdpressurelevels])
($method)($qualifiers) variable1z[i=@ave] 
ppl shaset protect

define view/xlim=0,1/ylim=0,0.1 colorbar
set v colorbar
go colorbar_put -h 10 90 10 50 0.3 ($qualifiers)

go logo_put ipsl

go textlayer_new
go textlayer_text_put 5 98 "@AS($basefile)" -1 0.35
go textlayer_text_put 5 95 "@AS($variable1)" -1 0.35
go textlayer_stat2_put 5 92 variable1z[i=@ave] -1 0.25


\cancel mode verify

!**************************************************************
! Author: Patrick Brockmann
! Contact: Patrick.Brockmann@ipsl.jussieu.fr
! History:
! Modification:
!  - derivated from LMDZ_1difffrom2variables_logzonal.jnl 
!    to be not a standalone script ie without page_new...
!
!**************************************************************

!************************************************************
def sym file1=($01)
def sym variable1=($02)
def sym vpressure1=($03)
def sym file2=($04)
def sym variable2=($05)
def sym vpressure2=($06)
def sym myqualifiers=($07)
def sym mymethod=($08)
def sym myvlim_min=($09)                          ! Must be > 0 and power of 10
def sym myvlim_max=($10)

!************************************************************
let variable1=($variable1)
let variable2=($variable2)

!===============================
use "($file1)"
go symbol_basename_and_define basefile1 "($file1)"
def sym variable1_lstart=`variable1,return=lstart`
def sym variable1_lend=`variable1,return=lend`
if `($variable1_lstart) ne ($variable1_lend)` then 
   say "Variable1 must have a time dimension equals to 1"  
   exit 
endif
let vpressure1_0=($vpressure1)
let vpressure1=vpressure1_0[d=($basefile1)]
use LMDZ_stdpressurelevels.nc
let variable1z = ZAXREPLACE(variable1[d=($basefile1)],vpressure1,Z[GZ=LMDZ_stdpressurelevels])

define axis/from_data/z/depth/units="mb" myzlog=log(Z[GZ=LMDZ_stdpressurelevels])
let myvariable1z = variable1z[gz=myzlog@asn]

!===============================
use "($file2)"
go symbol_basename_and_define basefile2 "($file2)"
def sym variable2_lstart=`variable2,return=lstart`
def sym variable2_lend=`variable2,return=lend`
if `($variable2_lstart) ne ($variable2_lend)` then 
   say "Variable2 must have a time dimension equals to 1"  
   exit 
endif
let vpressure2_0=($vpressure2)
let vpressure2=vpressure2_0[d=($basefile2)]
use LMDZ_stdpressurelevels.nc
let variable2z = ZAXREPLACE(variable2[d=($basefile2)],vpressure2,Z[GZ=LMDZ_stdpressurelevels])

define axis/from_data/z/depth/units="mb" myzlog=log(Z[GZ=LMDZ_stdpressurelevels])
let myvariable2z = variable2z[gz=myzlog@asn]

!===============================
let diff=(myvariable1z[d=($basefile1)]-myvariable2z[d=($basefile2)])

!------------------------
! Control number of digits in figures in the keybar
ppl shakey ,,,,4

ppl tics ,,0,0
ppl axlabp -1,0
($mymethod)/set_up/hlim=-90:90/vlim=`log(($myvlim_min))`:`log(($myvlim_max))`($myqualifiers) diff[i=@ave]

go unlabels 1 8 

ppl ($mymethod)

ppl window,off

if `($myvlim_min) lt 0.1 and ($myvlim_max) ge 0.1` then
   repeat/i=1:10:1 ( let myindice=i*1E-2 ; let myindicelog=log(myindice) ; \
        ppl aline 1,-93,`myindicelog`,-90,`myindicelog` ; \
        label -95,`myindicelog`,1,0,0.1,"`myindice`" ; \
        ppl aline 1,90,`myindicelog`,93,`myindicelog` })
endif
if `($myvlim_min) lt 1 and ($myvlim_max) ge 1` then
   repeat/i=1:10:1 ( let myindice=i*1E-1 ; let myindicelog=log(myindice) ; \
        ppl aline 1,-93,`myindicelog`,-90,`myindicelog` ; \
        label -95,`myindicelog`,1,0,0.1,"`myindice`" ; \
        ppl aline 1,90,`myindicelog`,93,`myindicelog` })
endif
if `($myvlim_min) lt 10 and ($myvlim_max) ge 10` then
   repeat/i=1:10:1 ( let myindice=i ; let myindicelog=log(myindice) ; \
        ppl aline 1,-93,`myindicelog`,-90,`myindicelog` ; \
        label -95,`myindicelog`,1,0,0.1,"`myindice`" ; \
        ppl aline 1,90,`myindicelog`,93,`myindicelog` })
endif
if `($myvlim_min) lt 100 and ($myvlim_max) ge 100` then
   repeat/i=1:10:1 ( let myindice=i*1E1 ; let myindicelog=log(myindice) ; \
        ppl aline 1,-93,`myindicelog`,-90,`myindicelog` ; \
        label -95,`myindicelog`,1,0,0.1,"`myindice`" ; \
        ppl aline 1,90,`myindicelog`,93,`myindicelog` })
endif
if `($myvlim_min) le 1000 and ($myvlim_max) ge 1000` then
   repeat/i=1:10:1 ( let myindice=i*1E2 ; let myindicelog=log(myindice) ; \
        ppl aline 1,-93,`myindicelog`,-90,`myindicelog` ; \
        label -95,`myindicelog`,1,0,0.1,"`myindice`" ; \
        ppl aline 1,90,`myindicelog`,93,`myindicelog` })
endif

label -105,`log(($myvlim_min))+(log(($myvlim_max))-log(($myvlim_min)))/2`,0,90,0.12,"HEIGHT (mb)"

! Return to default values
ppl window,on
ppl tics .125,.25,.125,.25
ppl axlabp -1,-1

!------------------------


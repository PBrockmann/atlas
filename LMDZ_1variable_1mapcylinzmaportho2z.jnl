
!**************************************************************
! Author: Patrick Brockmann
! Contact: Patrick.Brockmann@ipsl.jussieu.fr
! $Date: 2011/09/12 12:17:39 $
! $Name: ATLAS_672_1_1 $
! $Revision: 1.1.1.10 $
! History:
! Modification:
!
!**************************************************************

set memory/size=20

!************************************************************
def sym file=$1
def sym model=$2
def sym variable1=$3
def sym zlevel=$4
def sym qualifiers=/nolab/nokey/title=""$5% %
def sym polygonfile=$6%earth_continents%

def sym arg7=$7%none%
if ($arg7%|none>false|*>true|%) then
    def sym zoom=$7
else
    def sym zoom=/hlim=0:450:30/vlim=-90:90:30
endif

def sym arg8=$8%none%
if ($arg8%|none>false|*>true|%) then
    def sym cmdhgridlines=go hgridlines $8 
    def sym cmdvgridlines=go vgridlines $8
    def sym cmdgridortho2=go cont_ortho_2 0 90 /over 10 graticule_$8
else
    def sym cmdhgridlines=go null_command
    def sym cmdvgridlines=go null_command
    def sym cmdgridortho2=go null_command
endif

def sym surface_pressure_name=$9%PSOL%

!************************************************************
let variable1=($variable1)

use "($file)"
go symbol_basename_and_define basefile "($file)"

go page_new -i 1 2 0 100 0 90

set v 1
def sym CM=0 ! central meridien to set accordingly with zoom area
def sym variable1_lstart=`variable1,return=lstart`
def sym variable1_lend=`variable1,return=lend`
if `($variable1_lstart) ne ($variable1_lend)` then         
   say "Variable1 must have a time dimension equals to 1"  
   exit
endif
go LMDZ_define_pressure ($surface_pressure_name)[l=($variable1_lstart),d=($basefile)]
let variable1z = ZAXREPLACE(variable1[d=($basefile)],pressure,Z[GZ=LMDZ_stdpressurelevels])
go map_cylin ($model) ($CM) variable1z[z=($zlevel)] 20 ($qualifiers)($zoom) ($polygonfile)
ppl shaset protect
go map_cylin ($model) `($CM)+360` variable1z[z=($zlevel)] 20 /over($qualifiers)($zoom) ($polygonfile)
ppl shaset protect
($cmdhgridlines)
($cmdvgridlines)

set v 2
go map_ortho_2 ($model) 0 90 variable1z[z=($zlevel)] 10 ($qualifiers) ($polygonfile)
($cmdgridortho2)

define view/xlim=0,1/ylim=0.4,0.5 colorbar
set v colorbar
go colorbar_put -h 10 90 10 50 0.3 ($qualifiers)

go logo_put ipsl

go textlayer_new
go symbol_basename_and_define basefile "($file)"
go textlayer_text_put 5 98 "@AS($basefile)" -1 0.35
go textlayer_text_put 5 95 "@AS($variable1)[z=($zlevel)]" -1 0.35
go textlayer_stat_put 10 90 variable1z[z=($zlevel)] -1 0.25



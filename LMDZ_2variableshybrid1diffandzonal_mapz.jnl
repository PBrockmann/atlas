\cancel mode verify

!**************************************************************
! Author: Patrick Brockmann
! Contact: Patrick.Brockmann@ipsl.jussieu.fr
! $Date: 2011/09/12 12:17:39 $
! $Name: ATLAS_672_1_1 $
! $Revision: 1.1.1.9 $
! History:
! Modification:
!
!**************************************************************

set memory/size=20

!************************************************************
def sym title=$1
def sym file1=$2
def sym variable1=$3
def sym file2=$4
def sym variable2=$5
def sym zlevel=$6
def sym qualifiers=/nolab/pal=grads/title=""$7% %
def sym qualifiersdiff=/nolab/pal=grads/title=""$8% %
def sym qualifiersplot=/vgrat=(solid,black)/nolab/transpose/vlim=-90:90/color=1$9% %
def sym qualifiersplotdiff=/vgrat=(solid,black)/nolab/transpose/vlim=-90:90/color=1($10% %)
def sym surface_pressure_name=($11%PSOL%)

def sym viewlimits=[x=0:450,y=-90:90,z=($zlevel)]
def sym viewlimits2=[y=-90:90,z=($zlevel)]

def sym polygonfile1=earth_continents
def sym polygonfile2=earth_continents
def sym polygonfilediff=earth_continents

!************************************************************
let variable1_0=($variable1)
go symbol_basename_and_define basefile1 "($file1)"
let variable1=variable1_0[d=($basefile1)]
let variable2_0=($variable2)
go symbol_basename_and_define basefile2 "($file2)"
let variable2=variable2_0[d=($basefile2)]

def sym method=fill

go page_new -i 1 3 0 70 0 90 

set v 1 
use "($file1)"
def sym variable1_lstart=`variable1,return=lstart`
def sym variable1_lend=`variable1,return=lend`
if `($variable1_lstart) ne ($variable1_lend)` then 
   say "Variable1 must have a time dimension equals to 1"  
   exit 
endif
go LMDZ_define_pressure ($surface_pressure_name)[l=($variable1_lstart),d=($basefile1)]
let variable1z = ZAXREPLACE(variable1,pressure,Z[GZ=LMDZ_stdpressurelevels])
go margins_set 20 10 10 10
($method)($qualifiers) variable1z($viewlimits)
go cont_cylin 0 /over/nolab " " ($polygonfile1)
go text_put 5 90 "@AS($basefile1): ($variable1)" -1 0.35
go text_stat3_put 90 85 variable1z($viewlimits2) 1 0.25 

def grid/x=variable1z/y=variable1z/z=variable1z gridvar1

def view/xlim=0.7,1./ylim=0.6,0.9 4
set v 4 
go margins_set 20 10 10 15
plot/set_up($qualifiersplot) variable1z[i=@ave,z=($zlevel)]
! syntax: -1=bottom/left of plot, 0=no label, +1=top/right of plot. default=-1
ppl axlabp -1,1
ppl plot
use "($file2)"
def sym variable2_lstart=`variable2,return=lstart`
def sym variable2_lend=`variable2,return=lend`
if `($variable2_lstart) ne ($variable2_lend)` then 
   say "Variable2 must have a time dimension equals to 1"  
   exit 
endif
go LMDZ_define_pressure ($surface_pressure_name)[l=($variable2_lstart),d=($basefile2)]
let variable2z = ZAXREPLACE(variable2,pressure,Z[GZ=LMDZ_stdpressurelevels])
plot/color=2/nolab/transpose/over variable2z[i=@ave,z=($zlevel)]
go text_legend_put 10 25 87 " @AS($basefile1)" 1
go text_legend_put 10 25 83 " @AS($basefile2)" 2
! return to default
ppl axlabp -1,-1

set v 2 
use "($file2)"
go margins_set 20 10 10 10
($method)($qualifiers) variable2z($viewlimits)
go cont_cylin 0 /over/nolab " " ($polygonfile2)
go text_put 5 90 "@AS($basefile2): ($variable2)" -1 0.35
go text_stat3_put 90 85 variable2z($viewlimits2) 1 0.25

def view/xlim=0.7,1./ylim=0.3,0.6 5 
set v 5 
go margins_set 20 10 10 15
plot/set_up($qualifiersplot) variable2z[i=@ave,z=($zlevel)]
ppl axlabp -1,1
ppl plot
ppl axlabp -1,-1

set v 3 
let variable2z_regridded=variable2z[g=gridvar1]
let diff=variable1z-variable2z_regridded
go margins_set 20 10 10 10
($method)($qualifiersdiff) diff($viewlimits)
go cont_cylin 0 /over/nolab " " ($polygonfilediff)
go text_put 5 90 "@ASDifference: ($variable1) - ($variable2)" -1 0.35
go text_stat2_put 90 85 diff($viewlimits2) 1 0.25 

def view/xlim=0.7,1./ylim=0.0,0.3 6
set v 6 
go margins_set 20 10 10 15
plot/set_up($qualifiersplotdiff) diff[i=@ave,z=($zlevel)]
ppl axlabp -1,1
ppl plot
ppl axlabp -1,-1
ppl pen 1,2
ppl aline,1,0,($YAXIS_MIN),0,($YAXIS_MAX)
ppl pen 1,1

go logo_put ipsl

go textlayer_new
go textlayer_text_put 5 95 "@AS($title)" -1 0.5

!**************************************************************
set mode/last verify

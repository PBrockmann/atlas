
!**************************************************************
! Author: Patrick Brockmann
! Contact: Patrick.Brockmann@ipsl.jussieu.fr
! $Date: 2009/05/14 15:09:34 $
! $Name: ATLAS_608_1_5 $
! $Revision: 1.1.1.9 $
! History:
! Modification:
!
!**************************************************************

set memory/size=20

!************************************************************
def sym file=$1
def sym model=$2
def sym variable1=$3
def sym qualifiers=/nolab/nokey/title=""$4% %
def sym polygonfile=$5%earth_continents%

def sym arg6=$6%none% 
if ($arg6%|none>false|*>true|%) then
    def sym margins=25
    def sym zoom=$6
    ppl axlabp -1,-1
else
    def sym margins=5
    def sym zoom=/hlim=0:450:30/vlim=-90:90:30
    ppl axlabp 0,0
    ppl tics ,,,,1,1 
endif

def sym arg7=$7%none% 
if ($arg7%|none>false|*>true|%) then
    def sym cmdhgridlines=go hgridlines $7  
else
    def sym cmdhgridlines=go null_command 
endif

def sym arg8=$8%none% 
if ($arg8%|none>false|*>true|%) then
    def sym cmdvgridlines=go vgridlines $8  
else
    def sym cmdvgridlines=go null_command 
endif

!************************************************************
let variable1=($variable1)

use "($file)"
go symbol_basename_and_define basefile "($file)"

go page_new -i 1 1 0 100 10 90

def sym CM=0 ! central meridien 
def sym qualifiers=($qualifiers)($zoom)

set v 1
go map_cylin ($model) ($CM) variable1 ($margins) ($qualifiers) ($polygonfile)
ppl shaset protect
go map_cylin ($model) `($CM)+360` variable1 ($margins) ($qualifiers)/over ($polygonfile)
ppl shaset protect
($cmdhgridlines) 
($cmdvgridlines) 

define view/xlim=0,1/ylim=0,0.1 colorbar
set v colorbar
go colorbar_put -h 10 90 10 50 0.3 ($qualifiers)

go logo_put ipsl

go textlayer_new
go textlayer_text_put 5 96 "@AS($basefile)" -1 0.35
go textlayer_text_put 5 93 "@AS($variable1)" -1 0.35

use "($model)_grid.nc"
def sym kend=`variable1[d=($basefile)],return=kend`
let variable1_stat=if mask[d=($MODEL)_grid.nc,k=1:($kend)] eq 1 then variable1[d=($basefile)] 
go textlayer_text_put 5 90 "Global statistics:" -1 0.25
go textlayer_stat2_put 22 90 variable1_stat -1 0.25

